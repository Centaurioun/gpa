dnl
dnl Configure script source for PGA
dnl
dnl (Process this file with autoconf to produce a configure script.)
AC_REVISION($Revision$)dnl
AC_PREREQ(2.13)

AC_INIT(src/gpa.c)
AM_CONFIG_HEADER(config.h)

dnl
dnl The version number goes here
dnl
AM_INIT_AUTOMAKE(gpa,0.2.0)
ALL_LINGUAS=""

dnl
dnl Setup some stuff depending on host/target.
dnl
try_gettext=yes
case "${target}" in
    *-*-mingw32)
        # special stuff for Windoze NT
        # Do we need to set cross_compiling here or is it sufficient
        # to rely on AC_PROG_CC which is called later?
        CC="${target}-gcc"
        CPP="${target}-gcc -E"
        RANLIB="${target}-ranlib"
        try_gettext=no
        ;;
    *)
       ;;
esac


AM_MAINTAINER_MODE

dnl
dnl Checks for programs
dnl
AC_PROG_CC
AC_ISC_POSIX
AC_STDC_HEADERS
AC_ARG_PROGRAM
AC_PROG_RANLIB


dnl
dnl Checks for libraries
dnl

if test "$try_gettext" = yes; then
AM_GNU_GETTEXT
else
USE_NLS=no
USE_INCLUDED_LIBINTL=no
AC_SUBST(USE_NLS)
AC_SUBST(USE_INCLUDED_LIBINTL)
fi

dnl Where is the GTK+ toolkit
if test "$cross_compiling" = yes ; then
  CFLAGS="$CFLAGS -I/home/wk/work/gtk+w32/include/gtk+/gdk/win32 \
 -I/home/wk/work/gtk+w32/include -I/home/wk/work/gtk+w32/include/gtk+"
  LIBS="$LIBS     -L/home/wk/work/gtk+w32/lib -lgtk -lgdk -lglib"
else
  AM_PATH_GTK(1.2.1,
            [LIBS="$LIBS $GTK_LIBS"
            CFLAGS="$CFLAGS $GTK_CFLAGS"],
            AC_MSG_ERROR(Cannot find GTK+: Is gtk-config in executable path ?))
fi


dnl
dnl Checks for header files
dnl
dnl These are needed by libjnlib
AC_CHECK_FUNCS(memicmp stpcpy strlwr strtoul memmove stricmp)


dnl
dnl Checks for typedefs and structures
dnl

GNUPG_CHECK_TYPEDEF(byte, HAVE_BYTE_TYPEDEF)
GNUPG_CHECK_TYPEDEF(ushort, HAVE_USHORT_TYPEDEF)
GNUPG_CHECK_TYPEDEF(ulong, HAVE_ULONG_TYPEDEF)
GNUPG_CHECK_TYPEDEF(u16, HAVE_U16_TYPEDEF)
GNUPG_CHECK_TYPEDEF(u32, HAVE_U32_TYPEDEF)
# We should not use them in this software;
# However jnlib/types.h needs them - so we take the easy way.
AC_CHECK_SIZEOF(unsigned short, 2)
AC_CHECK_SIZEOF(unsigned int, 4)
AC_CHECK_SIZEOF(unsigned long, 4)
if test "$ac_cv_sizeof_unsigned_short" = "0" \
   || test "$ac_cv_sizeof_unsigned_int" = "0" \
   || test "$ac_cv_sizeof_unsigned_long" = "0"; then
    AC_MSG_WARN([Hmmm, something is wrong with the sizes - using defaults]);
fi



dnl
dnl Checks for compiler features
dnl

dnl
dnl Checks for library functions
dnl




dnl
dnl Checks for system services
dnl


if test "$GCC" = yes; then
    CFLAGS="$CFLAGS -Wall -Wcast-align -Wshadow -Wstrict-prototypes"
fi


AC_OUTPUT([
Makefile
intl/Makefile
po/Makefile.in
jnlib/Makefile
gpapa/Makefile
src/Makefile
])

dnl *-*wedit:notab*-*  Please keep this as the last line.
